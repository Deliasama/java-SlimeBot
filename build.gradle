import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'

    id 'application'
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

mainClassName = "de.slimecloud.slimeball.main.Main"

group 'com.slimebot'
version '3.0.0'

repositories {
    mavenCentral()
    maven { url 'https://maven.mineking.dev/releases' }
    maven { url 'https://maven.mineking.dev/snapshots' }

    maven { url 'https://jitpack.io/' }
}

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-beta.18'

    implementation 'de.mineking:DiscordUtils:3.6.0'
    implementation 'de.mineking:JavaUtils:1.0.9'
    implementation 'de.cyklon:JEvent:1.0.2'

    implementation 'se.michaelthelin.spotify:spotify-web-api-java:8.0.0'
    implementation 'com.github.minndevelopment:emoji-java:master-SNAPSHOT'
    implementation 'org.kohsuke:github-api:1.315'

    implementation 'io.github.cdimascio:java-dotenv:5.1.1'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'org.sejda.imageio:webp-imageio:0.1.6'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.4.8'

    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'org.jdbi:jdbi3-postgres:3.39.0'

    compileOnly 'org.jetbrains:annotations:13.0'

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

String getVersion() {
    String version = project.version.toString()

    try {
        String commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        version += "-" + commit
    } catch(IOException ignore) {
        version += "-unknown"
    }

    return version
}

tasks.register("prepareSources", Copy) {
    delete("$buildDir/preparedSources")

    from("src/main/java") {
        include("**/BuildInfo.java")

        filter(ReplaceTokens, "tokens":
                Map.of(
                        "VERSION", getVersion()
                )
        )
    }

    into("$buildDir/preparedSources")

    includeEmptyDirs = false
}

compileJava {
    FileTree javaSources = sourceSets.main.allJava.filter {
        it.name != "BuildInfo.java"
    }.asFileTree

    source = javaSources + fileTree("$buildDir/preparedSources")

    dependsOn "prepareSources"

    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}


test {
    useJUnitPlatform()
    systemProperty "file.encoding", "utf-8"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}